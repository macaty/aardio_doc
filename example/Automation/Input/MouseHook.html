
<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<meta name="robots" content="index, follow">
<meta name='description' content='aardio 编程语言范例 - 鼠标钩子演示 - 请按下鼠标键测试'>
<meta http-equiv='content-language' content='zh-cn'>
<title>aardio 编程语言范例 - 鼠标钩子演示 - 请按下鼠标键测试</title> 
<link rel="stylesheet" href="../../../css/markdown.css" tppabs="https://www.aardio.com/zh-cn/doc/css/markdown.css">
<script src="../../../js/prism.js" tppabs="https://www.aardio.com/zh-cn/doc/js/prism.js"></script>
<link rel="stylesheet" href="../../../../../../lib.baomitu.com/font-awesome/6.6.0/css/fontawesome.min.css" tppabs="https://lib.baomitu.com/font-awesome/6.6.0/css/fontawesome.min.css">
<link rel="stylesheet" href="../../../../../../lib.baomitu.com/font-awesome/6.6.0/css/solid.min.css" tppabs="https://lib.baomitu.com/font-awesome/6.6.0/css/solid.min.css">
</head>
<body class="markdown-body"><a id="back-to-home" href="../../../index.htm" tppabs="https://www.aardio.com/zh-cn/doc/" title="aardio 编程语言文档首页"><i class="fas fa-home" id="home-icon"></i><i class="fas fa-robot" id="ai-icon" style="display: none;"></i><span id="aardio-document-home">aardio 文档</span></a>
<h1>aardio 范例: 鼠标钩子演示 - 请按下鼠标键测试</h1><pre><code class="aardio language-aardio">import win.ui;
/*DSG{{*/
var winform = win.form(text=&quot;鼠标钩子演示 - 请按下鼠标键测试&quot;;right=438;bottom=396;max=false;topmost=1)
winform.add(
edit={cls=&quot;edit&quot;;left=13;top=12;right=426;bottom=382;edge=1;multiline=1;z=1}
)
/*}}*/

import mouse.hook;
var hook = mouse.hook();

//录制回调函数
hook.proc = function(msg,x,y,mouseData,injected,flags,timeStamp,extraInfo){
    if( injected ) return; //模拟鼠标不处理

    select(msg) {
        case 0x201/*_WM_LBUTTONDOWN*/{
            winform.edit.print(&quot;左键按下&quot;,x,y)
        }
        case 0x202/*_WM_LBUTTONUP*/{
            winform.edit.print(&quot;左键弹起&quot;,x,y)
        }
        case 0x204/*_WM_RBUTTONDOWN*/{
            winform.edit.print(&quot;右键按下&quot;,x,y)
        }
        case 0x205/*_WM_RBUTTONUP*/{
            winform.edit.print(&quot;右键弹起&quot;,x,y)
        }
        case 0x207/*_WM_MBUTTONDOWN*/{
            winform.edit.print(&quot;中键按下&quot;,x,y)
        }
        case 0x208/*_WM_MBUTTONUP*/{
            winform.edit.print(&quot;中键弹起&quot;,x,y) 
        }  
        case 0x20B/*_WM_XBUTTONDOWN*/{
            winform.edit.print(&quot;侧键按下&quot;,x,y,::HIWORD(mouseData) == 1 ? &quot;XBUTTON1&quot; : &quot;XBUTTON2&quot;)   
        }
        case 0x20C/*_WM_XBUTTONUP*/{
            winform.edit.print(&quot;侧键弹起&quot;,x,y,::HIWORD(mouseData) == 1 ? &quot;XBUTTON1&quot; : &quot;XBUTTON2&quot;)   
        }
    }   
} 

winform.wndproc = function(hwnd,message,wparam,lparam){ 
    if( message = 0xA1/*_WM_NCLBUTTONDOWN*/ ){
        if( wparam == 20/*_HTCLOSE*/ ){ 
            //鼠标按下关闭按钮、释放鼠标以前及时关闭钩子,避免 XP 系统关闭窗体时出现的卡顿
            hook.close() 
        }
    }  
}  

/**details(限制)
低级鼠标钩子回调函数不要做耗时操作，
如果耗时超过一秒或超过注册表限制的更小时间，系统会直接删除钩子。
没有任何方法可以检查到删除操作，应当遵守规则避免滥用。

耗时操作建议在钩子回调中创建线程，或使用 winform.setTimeout 延时异步执行。
在超级热键回调函数中，如果返回一个函数也可以不阻塞异步执行。

如果仍然出现过一段时间键盘钩子失效。
建议检查一下是否安装了什么特别的软件导致了这样的问题。
end details**/ 

winform.show(true) 
win.loopMessage();  
</code></pre>
<a href="MouseHook.md" tppabs="https://www.aardio.com/zh-cn/doc/example/Automation/Input/MouseHook.md">Markdown 格式</a>
</body> 
</html>