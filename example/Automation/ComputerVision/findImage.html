
<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<meta name="robots" content="index, follow">
<meta name='description' content='aardio 编程语言范例 - 找图代码生成器'>
<meta http-equiv='content-language' content='zh-cn'>
<title>aardio 编程语言范例 - 找图代码生成器</title> 
<link rel="stylesheet" href="../../../css/markdown.css" tppabs="https://www.aardio.com/zh-cn/doc/css/markdown.css">
<script src="../../../js/prism.js" tppabs="https://www.aardio.com/zh-cn/doc/js/prism.js"></script>
<link rel="stylesheet" href="../../../../../../lib.baomitu.com/font-awesome/6.6.0/css/fontawesome.min.css" tppabs="https://lib.baomitu.com/font-awesome/6.6.0/css/fontawesome.min.css">
<link rel="stylesheet" href="../../../../../../lib.baomitu.com/font-awesome/6.6.0/css/solid.min.css" tppabs="https://lib.baomitu.com/font-awesome/6.6.0/css/solid.min.css">
</head>
<body class="markdown-body"><a id="back-to-home" href="../../../index.htm" tppabs="https://www.aardio.com/zh-cn/doc/" title="aardio 编程语言文档首页"><i class="fas fa-home" id="home-icon"></i><i class="fas fa-robot" id="ai-icon" style="display: none;"></i><span id="aardio-document-home">aardio 文档</span></a>
<h1>aardio 范例: 找图代码生成器</h1><pre><code class="aardio language-aardio">//找图代码生成器
import win.ui;
/*DSG{{*/
var winform = win.form(text=&quot;找图代码生成器&quot;;right=454;bottom=403)
winform.add(
btnSnap={cls=&quot;button&quot;;text=&quot;选择要查找的目标图像&quot;;left=146;top=324;right=450;bottom=400;color=14120960;db=1;dl=1;dr=1;font=LOGFONT(h=-14);note=&quot;在屏幕上框选要查找的目标图像，
目标图像越小，特征越明显，找图效果越好&quot;;z=3};
btnTest={cls=&quot;button&quot;;text=&quot;运行测试&quot;;left=325;top=7;right=418;bottom=34;dr=1;dt=1;z=5};
editCode={cls=&quot;edit&quot;;left=5;top=40;right=443;bottom=319;db=1;dl=1;dr=1;dt=1;edge=1;hscroll=1;multiline=1;vscroll=1;z=2};
plusImage={cls=&quot;plus&quot;;left=7;top=337;right=106;bottom=385;db=1;dl=1;repeat=&quot;scale&quot;;z=1};
static3={cls=&quot;static&quot;;text=&quot;自动找图代码：&quot;;left=8;top=14;right=244;bottom=37;dl=1;dt=1;transparent=1;z=4}
)
/*}}*/

import mouse;
import soImage; 
import winex;
winform.btnSnap.oncommand = function(id,event){

    import gdip.snap;
    import mouse.screenArea;
    var screenArea = mouse.screenArea();
    screenArea.onSelectionChanged = function(rc){
        //尽量截取特征明显的图
        var bmp = gdip.snap(screenArea.hwnd,rc);
        var buf = bmp.saveToBuffer(&quot;*.jpg&quot;,80);
        winform.plusImage.background = buf;
        var strJpg = ..string.escape(buf);

        owner.close();  

        var x,y = mouse.getPos();  
        winex.waitClose(screenArea.hwnd); 

        var hwndTarget = winex.fromPointReal( x,y,0x0004/*_CWP_SKIPTRANSPARENT*/ | 0x0001/*_CWP_SKIPINVISIBLE*/  );
        var hwndRoot = win.getRoot(hwndTarget);
        var pClass,pText = winform.validPattern(win.getClass(hwndTarget)) ,winform.validPattern(winex.getText(hwndTarget,50));

        var hwndPattern = string.format(&#39;var hwndParent = winex.find(&quot;%s&quot;,&quot;%s&quot;);&#39;,pClass,pText);
        if( hwndRoot != hwndTarget ){ 
            var pClass2,pText2 = winform.validPattern(win.getClass(hwndRoot)) ,winform.validPattern(winex.getText(hwndRoot,50));
            hwndPattern = string.format(&#39;var hwndParent = winex.find(&quot;%s&quot;,&quot;%s&quot;);\r\nvar hwnd = winex.findEx(hwndParent,,&quot;%s&quot;,&quot;%s&quot;);&#39;,pClass2,pText2,pClass,pText); 
        }

        import string.template;
        var strCode = string.template();
        strCode.template = /***
//创建要查找的目标图像
import soImage;
var imgFind = soImage();
imgFind.setBytes(&#39;${jpg}&#39;);

//查找窗口
import winex;
${hwnd}

//窗口截图
var imgWindow = soImage();
imgWindow.captureWindow(hwnd);

//查找目标图像
var sm,x,y = imgFind.findImage( imgWindow );

//移动鼠标
import mouse; 
mouse.moveToWindow(0,0,hwnd);
mouse.moveToWindow(x,y,hwnd,8);
        ***/

        winform.editCode.text =  strCode.format(
            hwnd = hwndPattern;
            jpg = strJpg; 
        ); 
    }
    screenArea.doModal(); 
}

winform.btnTest.oncommand = function(id,event){
    code = string.trim( winform.editCode.text );
    if(!#code){
        winform.msgboxErr(&quot;请先选择目标图像&quot;);
        return;
    }

    loadcodex(code);
}

winform.validPattern = function(str){
    if(!#str) return &quot;&quot;;

    var mbs = string.match(str,&quot;[\s\w]*:+[\s\w]*&quot;)
    if(#mbs)
        return mbs;

    str = string.replace(str,&quot;(\p)&quot;,&quot;\\\1&quot;)     
    str = string.replace(str,&quot;\x+&quot;,function(str){
        if(..string.find(str,&quot;\d&quot;) ) return &quot;\x+&quot;
        return str;
    } )     

    str = string.replace(str,&quot;\d+&quot;,&quot;\\d+&quot;)  
    return str;
}


winform.show();
win.loopMessage();
</code></pre>
<a href="findImage.md" tppabs="https://www.aardio.com/zh-cn/doc/example/Automation/ComputerVision/findImage.md">Markdown 格式</a>
</body> 
</html>