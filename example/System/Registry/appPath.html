
<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<meta name="robots" content="index, follow">
<meta name='description' content='aardio 编程语言范例 - 注册表操作 - 添加系统搜索路径'>
<meta http-equiv='content-language' content='zh-cn'>
<title>aardio 编程语言范例 - 注册表操作 - 添加系统搜索路径</title> 
<link rel="stylesheet" href="../../../css/markdown.css" tppabs="https://www.aardio.com/zh-cn/doc/css/markdown.css">
<script src="../../../js/prism.js" tppabs="https://www.aardio.com/zh-cn/doc/js/prism.js"></script>
<link rel="stylesheet" href="../../../../../../lib.baomitu.com/font-awesome/6.6.0/css/fontawesome.min.css" tppabs="https://lib.baomitu.com/font-awesome/6.6.0/css/fontawesome.min.css">
<link rel="stylesheet" href="../../../../../../lib.baomitu.com/font-awesome/6.6.0/css/solid.min.css" tppabs="https://lib.baomitu.com/font-awesome/6.6.0/css/solid.min.css">
</head>
<body class="markdown-body"><a id="back-to-home" href="../../../index.htm" tppabs="https://www.aardio.com/zh-cn/doc/" title="aardio 编程语言文档首页"><i class="fas fa-home" id="home-icon"></i><i class="fas fa-robot" id="ai-icon" style="display: none;"></i><span id="aardio-document-home">aardio 文档</span></a>
<h1>aardio 范例: 注册表操作 - 添加系统搜索路径</h1><pre><code class="aardio language-aardio">//注册表操作 - 添加系统搜索路径
import win.ui;
import win.dlg.message;
/*DSG{{*/
var winform = win.form(text=&quot;添加到系统搜索路径&quot;;right=640;bottom=225;max=false)
winform.add(
btnAdd={cls=&quot;button&quot;;text=&quot;添加到系统搜索路径&quot;;left=25;top=114;right=474;bottom=181;color=14120960;font=LOGFONT(h=-14);note=&quot;添加后可按 Win + S 输入名字或在资源管理器地址栏直接输入名字打开&quot;;z=1};
cmbPath={cls=&quot;combobox&quot;;left=60;top=49;right=581;bottom=73;edge=1;items={};mode=&quot;dropdown&quot;;z=2};
static={cls=&quot;static&quot;;text=&quot;可输入 EXE 名搜索运行过的程序路径：&quot;;left=60;top=26;right=485;bottom=45;color=6974058;transparent=1;z=3}
)
/*}}*/

import win.debounce;
import process.cache;
winform.cmbPath.onEditChange  = win.debounce(function(){
    var filename = winform.cmbPath.text;
    filename = string.trim(filename,` &quot;`);

    if(io.exist(filename)) return;

    var lst = process.cache.list(false);
    var files = table.filter(lst,function(v,index){
        var f = io.splitpath(v).file;
        return(f &amp;&amp;..string.startWith(f,filename,true));
    }) 

    winform.cmbPath.autoComplete(files:{},1) //更新下拉列表   
},300)

import sys.reg;
winform.btnAdd.oncommand = function(id,event){
    if(!io.exist(winform.cmbPath.text)){
        return winform.cmbPath.editBox.showErrorTip(&quot;请指定有效程序路径&quot;);
    }

    /*
    不用复制文件到系统目录，不用改环境变量，不需要管理权限。
    简单的改一下注册表，就可实现按 Win + S 输入名字或在资源管理器地址栏直接输入名字打开程序
    */
    var tPath = io.splitpath(winform.cmbPath.text);
    sys.reg.setValue(&quot;&quot;,winform.cmbPath.text,`SOFTWARE\Microsoft\Windows\CurrentVersion\App Paths\` + tPath.file);

    winform.msgGreat(&quot;已添加到系统搜索路径&quot;);
}

winform.cmbPath.editBox.modifyStyle(,2/*_ES_RIGHT*/);//右对齐

//拖动文件到窗口
winform.onDropFiles = function(files){
    winform.cmbPath.text = files[1];
}

winform.btnAdd.setFocus();

winform.show();
win.loopMessage();
</code></pre>
<a href="https://www.aardio.com/zh-cn/doc/example/System/Registry/appPath.md">Markdown 格式</a>
</body> 
</html>