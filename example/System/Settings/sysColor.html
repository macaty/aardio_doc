
<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<meta name="robots" content="index, follow">
<meta name='description' content='aardio 编程语言范例 - 系统窗口背景色设置工具'>
<meta http-equiv='content-language' content='zh-cn'>
<title>aardio 编程语言范例 - 系统窗口背景色设置工具</title> 
<link rel="stylesheet" href="../../../css/markdown.css" tppabs="https://www.aardio.com/zh-cn/doc/css/markdown.css">
<script src="../../../js/prism.js" tppabs="https://www.aardio.com/zh-cn/doc/js/prism.js"></script>
<link rel="stylesheet" href="../../../../../../lib.baomitu.com/font-awesome/6.6.0/css/fontawesome.min.css" tppabs="https://lib.baomitu.com/font-awesome/6.6.0/css/fontawesome.min.css">
<link rel="stylesheet" href="../../../../../../lib.baomitu.com/font-awesome/6.6.0/css/solid.min.css" tppabs="https://lib.baomitu.com/font-awesome/6.6.0/css/solid.min.css">
</head>
<body class="markdown-body"><a id="back-to-home" href="../../../index.htm" tppabs="https://www.aardio.com/zh-cn/doc/" title="aardio 编程语言文档首页"><i class="fas fa-home" id="home-icon"></i><i class="fas fa-robot" id="ai-icon" style="display: none;"></i><span id="aardio-document-home">aardio 文档</span></a>
<h1>aardio 范例: 系统窗口背景色设置工具</h1><pre><code class="aardio language-aardio">//系统窗口背景色
import win.ui;
import win.ui.ctrl.pick;
/*DSG{{*/
var winform = win.form(text=&quot;系统窗口背景色设置工具&quot;;right=742;bottom=472;bgcolor=16777215;border=&quot;thin&quot;;composited=1;exmode=&quot;toolwindow&quot;;max=false;min=false)
winform.add(
chkSaveReg={cls=&quot;checkbox&quot;;text=&quot;在注册表中保存窗口背景色设置，以避免重启或注销后失效&quot;;left=20;top=9;right=386;bottom=31;bgcolor=16777215;z=2};
colorPick={cls=&quot;pick&quot;;text=&quot;自定义控件&quot;;left=17;top=47;right=718;bottom=450;z=1}
)
/*}}*/

//颜色改变时触发此事件
winform.colorPick.onColorChange = function(clr){

    if( winform.timerSetSysColor ) winform.clearInterval(winform.timerSetSysColor)
    winform.timerSetSysColor = winform.setTimeout( 
        function(){
            var r,g,b,a = color.getRgba(clr);
            var rgb = color.rgb(r,g,b);

            if( a != 0xFF) {
                winform.colorPick.setColor(clr,true);  
            }

            ::User32.SetSysColors(1, 
                { int elements[] = { 0x5/*_COLOR_WINDOW*/ };  } , 
                {INT colors[] = {rgb};}
            );   

            if( winform.chkSaveReg.checked ){
                import win.reg;
                var reg = win.reg(&quot;HKEY_CURRENT_USER\Control Panel\Colors&quot;);
                reg.setSzValue(&quot;Window&quot;,string.format(&quot;%d %d %d&quot;,r,g,b));
                reg.close();
            }
        },300)
 }

//可选指定初始颜色
winform.colorPick.setColor(  ::User32.GetSysColor(0x5/*_COLOR_WINDOW*/),true );

winform.chkSaveReg.oncommand = function(id,event){
    if( winform.chkSaveReg.checked ){
        import win.reg;
        var reg = win.reg(&quot;HKEY_CURRENT_USER\Control Panel\Colors&quot;); 
        reg.setSzValue(&quot;Window&quot;,string.format(&quot;%d %d %d&quot;, winform.colorPick.getRgba() ));
        reg.close();
    }
}

import ide;
win.setOwner(winform.hwnd,ide.getMainHwnd());  
winform.onClose = function(hwnd,message,wParam,lParam){
    win.setOwner(winform.hwnd,0)
}

winform.show() 
win.loopMessage();
</code></pre>
<a href="https://www.aardio.com/zh-cn/doc/example/System/Settings/sysColor.md">Markdown 格式</a>
</body> 
</html>