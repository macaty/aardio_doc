
<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<meta name="robots" content="index, follow">
<meta name='description' content='aardio 编程语言范例 - 电源管理关屏'>
<meta http-equiv='content-language' content='zh-cn'>
<title>aardio 编程语言范例 - 电源管理关屏</title> 
<link rel="stylesheet" href="../../../css/markdown.css" tppabs="https://www.aardio.com/zh-cn/doc/css/markdown.css">
<script src="../../../js/prism.js" tppabs="https://www.aardio.com/zh-cn/doc/js/prism.js"></script>
<link rel="stylesheet" href="../../../../../../lib.baomitu.com/font-awesome/6.6.0/css/fontawesome.min.css" tppabs="https://lib.baomitu.com/font-awesome/6.6.0/css/fontawesome.min.css">
<link rel="stylesheet" href="../../../../../../lib.baomitu.com/font-awesome/6.6.0/css/solid.min.css" tppabs="https://lib.baomitu.com/font-awesome/6.6.0/css/solid.min.css">
</head>
<body class="markdown-body"><a id="back-to-home" href="../../../index.htm" tppabs="https://www.aardio.com/zh-cn/doc/" title="aardio 编程语言文档首页"><i class="fas fa-home" id="home-icon"></i><i class="fas fa-robot" id="ai-icon" style="display: none;"></i><span id="aardio-document-home">aardio 文档</span></a>
<h1>aardio 范例: 电源管理关屏</h1><pre><code class="aardio language-aardio">//电源管理关屏
import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text=&quot;自动关屏&quot;;left=-20;top=50;right=319;bottom=99;bgcolor=16777215;border=&quot;none&quot;;max=false)
winform.add(
btnClose={cls=&quot;close&quot;;text=&quot;×&quot;;left=297;top=11;right=325;bottom=39;bgcolor=13405076;dr=1;dt=1;font=LOGFONT(h=-21);z=3};
btnCloseLcd={cls=&quot;plus&quot;;text=&quot;关屏&quot;;left=191;top=14;right=240;bottom=33;color=8388608;notify=1;textPadding={left=5};z=5};
chkLock={cls=&quot;plus&quot;;text=&quot;锁屏&quot;;left=242;top=14;right=296;bottom=33;align=&quot;left&quot;;iconStyle={align=&quot;left&quot;;font=LOGFONT(name=&#39;FontAwesome&#39;)};iconText=&#39;\uF0C8 &#39;;notify=1;textPadding={left=15};z=4};
datetimepick={cls=&quot;datetimepick&quot;;left=111;top=14;right=186;bottom=38;edge=1;transparent=1;updown=1;z=1};
lbTitle={cls=&quot;static&quot;;text=&quot;空闲时间超过：&quot;;left=0;top=18;right=103;bottom=40;align=&quot;right&quot;;transparent=1;z=2}
)
/*}}*/

import win.ui.tooltip
var balloonTipCtrl = win.ui.tooltip.tracking(winform,false)

import sys.power;
winform.datetimepick.onDateTimeChanged = function(dateTime){

    if(tonumber(dateTime)&lt;5){
        var x,y,cx,cy = winform.datetimepick.getPos(true);
        balloonTipCtrl.setText(&quot;小于 5 秒，已禁用自动关屏。&quot;).trackPopup(true,x+20,y+cy );

        sys.power.setMonitorTimeout(0);
    }
    else {
        balloonTipCtrl.trackPopup(false);
        sys.power.setMonitorTimeout(tonumber(dateTime));
    } 
}

var timeout = sys.power.getMonitorTimeout() : 0;
winform.datetimepick.time = time.iso8601(timeout);
if(timeout&lt;5) winform.datetimepick.onDateTimeChanged(timeout);
winform.datetimepick.setFormat(&quot;&#39; &#39;HH&#39;:&#39;mm&#39;:&#39;ss&quot;);

import fsys.config;
var config = fsys.config(io.appData(&quot;aardio/std/closeLcd&quot;))

//注册电源设置事件通知窗体
import sys.power.notification;
var sn = sys.power.notification(winform,&quot;6FE69556-704A-47A0-8F24-C28D936FDA47&quot;);

import sys;
sn.onPowerSettingChange = function(guid,data){
    if(data == 0 &amp;&amp; config.setting.lock){
        sys.lock(); //关屏时自动锁屏
    }
}

winform.onMouseDown = function(){
    balloonTipCtrl.trackPopup(false);
    winform.hitCaption();
}

winform.chkLock.oncommand = function(id,event){
    config.setting.lock = winform.chkLock.checked;
    config.setting.save();
}

//主动关屏
winform.btnCloseLcd.oncommand = function(id,event){
    //延时异步执行，避免显示器刚关掉又被唤醒
    winform.setTimeout( 
        function(){ 
            //用:User32.SendMessage会卡，改用异步的::User32.SendNotifyMessage
            ::User32.SendNotifyMessage( 0xFFFF/*_HWND_BROADCAST*/ ,0x112/*_WM_SYSCOMMAND*/, 0xF170/*_SC_MONITORPOWER*/ ,2);
        },200 //给一点延时，万一鼠标还在晃呢?
    )   
}

winform.chkLock.checked = config.setting.lock;

winform.btnCloseLcd.skin({
    color={
        active=0xFF00FF00;
        default=0xFF000080;
        disabled=0xFF6D6D6D;
        hover=0xFFFF0000
    }
})

winform.chkLock.skin(
    color = { 
        hover = 0xFFFF0000; 
        active = 0xFF00FF00;
    }
    checked = { 
        iconText = &#39;\uF14a&#39;;
    } 
)  

//点击 close 控件关闭窗口。
winform.btnClose.oncommand = function(id,event){
    winform.close();
}
winform.btnClose.show(true);

import win.ui.shadow;
win.ui.shadow(winform);

winform.show(); 
win.loopMessage();
</code></pre>
<a href="https://www.aardio.com/zh-cn/doc/example/System/Hardware/monitorPower.md">Markdown 格式</a>
</body> 
</html>